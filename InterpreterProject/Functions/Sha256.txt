// Custom ror function that deals with signed integers.
function ror(x, n)
{
    sbit = 0
    if (x<0)
    {
        sbit = 1
    }

    r = x & 0x7fffffff
    r = (r >> n) | (r << (32-n))
    if (sbit == 1)
    {
        r = r | (1 << (31-n))
    }

    return(r)
}

// Custom shr function that deals with signed integers.
function shr(x, n)
{
    sbit = 0
    if (x<0)
    {
        sbit = 1
    }

    r = x & 0x7fffffff
    r = r >> n

    if (sbit == 1)
    {
        r = r | (1 <<(31-n))
    }
    return(r)
}

// Custom add function deals with signed integers.
function add(a,b)
{
    a1 = a & 0x7fffffff
    b2 = b & 0x7fffffff
    c1 = (a1 + b2) & 0x7fffffff
    abit = 0
    if (a<0)
    {
        abit = 1
    }

    bbit = 0
    if (b<0)
    {
        bbit = 1
    }

    cbit = 0
    if ((a1+b2)<0)
    {
        cbit = 1
    }

    sumbit = abit + bbit + cbit
    if ((sumbit & 1)==1)
    {
        c1 = c1 | 0x80000000
    }

    return(c1)
}

function DumpMsg(m, count)
{
    for (i=0, i < count, i=i+1)
    {
        print(width=2, fill=0, hex, m[i], " ")
    }
    print(endl)
}

// msg is an array of ints, each one is a byte long.
function Sha256(&msg)
{
    // Initialize hash values
    // first 32 bits of the fractional parts of the square roots of the first 8 primes 2 through 19
    h0 = 0x6a09e667
    h1 = 0xbb67ae85
    h2 = 0x3c6ef372
    h3 = 0xa54ff53a
    h4 = 0x510e527f
    h5 = 0x9b05688c
    h6 = 0x1f83d9ab
    h7 = 0x5be0cd19

    // Initialize array of round constants
    // first 32 bits of the fractional parts of the cube roots of the first 64 primes 2 through 311
    k = dim[64]
    k = { 0x428a2f98, 0x71374491, 0xb5c0fbcf, 0xe9b5dba5, 0x3956c25b, 0x59f111f1, 0x923f82a4, 0xab1c5ed5,
          0xd807aa98, 0x12835b01, 0x243185be, 0x550c7dc3, 0x72be5d74, 0x80deb1fe, 0x9bdc06a7, 0xc19bf174,
          0xe49b69c1, 0xefbe4786, 0x0fc19dc6, 0x240ca1cc, 0x2de92c6f, 0x4a7484aa, 0x5cb0a9dc, 0x76f988da,
          0x983e5152, 0xa831c66d, 0xb00327c8, 0xbf597fc7, 0xc6e00bf3, 0xd5a79147, 0x06ca6351, 0x14292967,
          0x27b70a85, 0x2e1b2138, 0x4d2c6dfc, 0x53380d13, 0x650a7354, 0x766a0abb, 0x81c2c92e, 0x92722c85,
          0xa2bfe8a1, 0xa81a664b, 0xc24b8b70, 0xc76c51a3, 0xd192e819, 0xd6990624, 0xf40e3585, 0x106aa070,
          0x19a4c116, 0x1e376c08, 0x2748774c, 0x34b0bcb5, 0x391c0cb3, 0x4ed8aa4a, 0x5b9cca4f, 0x682e6ff3,
          0x748f82ee, 0x78a5636f, 0x84c87814, 0x8cc70208, 0x90befffa, 0xa4506ceb, 0xbef9a3f7, 0xc67178f2}
    
    // Padding
    msgLenBits = len(msg) * 8

    // Determine kpad
    n = 0
    kpadbits = 512 * n - msgLenBits - 1 - 64
    while ( kpadbits < 0)
    {
        n = n + 1
        kpadbits = 512 * n - msgLenBits - 1 - 64
    }

    // Create a new msg with the pad
    msgWithPadLenBytes = (msgLenBits + 1 + kpadbits + 64) / 8
    msgWithPad = dim[msgWithPadLenBytes]
    for (i=0, i < len(msg), i = i+1)
    {
        msgWithPad[i] = msg[i]
    }

    kpadbytes = (1+kpadbits) / 8
    currBytes = len(msg)
    msgWithPad[currBytes] = 0x80
    currBytes = currBytes + 1

    for (i=1, i < kpadbytes, i = i + 1)
    {
        msgWithPad[currBytes] = 0
        currBytes = currBytes + 1
    }

    // Write the 64-bit length
    for (i=0, i < 4, i = i + 1)
    {
        msgWithPad[currBytes] = 0
        currBytes = currBytes + 1
    }

    msgWithPad[currBytes] = (msgLenBits >> 24 ) & 0xff
    currBytes = currBytes + 1

    msgWithPad[currBytes] = (msgLenBits >> 16 ) & 0xff
    currBytes = currBytes + 1

    msgWithPad[currBytes] = (msgLenBits >> 8 ) & 0xff
    currBytes = currBytes + 1

    msgWithPad[currBytes] = (msgLenBits) & 0xff
    currBytes = currBytes + 1

    print("Msg Length(L) Bits:  ", msgLenBits, endl)
    print("K Length      Bits:  ", kpadbits, endl)
    print("L+1+K+64      Bits:  ", msgLenBits + 1 + kpadbits + 64, endl)
    print("currBytes         :  ", currBytes, endl)
    print("Message with Pad:", endl)
    DumpMsg(msgWithPad, currBytes)

    // Process the message in successive 512-bit chunks
    w = dim[64]
    chunkCount = msgWithPadLenBytes / 64
    for (chunk = 0, chunk < chunkCount, chunk = chunk + 1)
    {
        print("Processing chunk:  ", chunk, endl)
        chunkStart = chunk * 64
        for (i=0, i < 16, i=i+1)
        {
            word = msgWithPad[chunkStart + i * 4] << 24
            word = word | (msgWithPad[chunkStart + i * 4 + 1] << 16)
            word = word | (msgWithPad[chunkStart + i * 4 + 2] << 8)
            word = word | msgWithPad[chunkStart + i * 4 + 3]
            w[i] = word
        }

        print("Extending the first 16 words...", endl)
        for (i=16, i < 64, i=i+1)
        {
            s0 = ror(w[i-15], 7) ^ ror(w[i-15],18) ^ shr(w[i-15],3)
            s1 = ror(w[i-2], 17) ^ ror(w[i-2], 19) ^ shr(w[i-2],10)
            t1 = add(w[i-16], s0)
            t1 = add(t1, w[i-7])
            t1 = add(t1, s1)
            w[i] = t1
        }

        a = h0
        b = h1
        c = h2
        d = h3
        e = h4
        f = h5
        g = h6
        h = h7

        print("Compression function main loop...", endl)
        for (i=0, i < 64, i=i+1)
        {
            s1 = ror(e,6) ^ ror(e,11) ^ ror(e,25)
            ch = (e & f) ^ ((~e) & g)
            t1 = add(h,s1)
            t1 = add(t1, ch)
            t1 = add(t1, k[i])
            temp1 = add(t1, w[i])
            s0 = ror(a,2) ^ ror(a,13) ^ ror(a,22)
            maj = (a&b) ^ (a&c) ^ (b&c)
            temp2 = add(s0, maj)

            h = g
            g = f
            f = e
            e = add(d, temp1)
            d = c
            c = b
            b = a
            a = add(temp1, temp2)
        }

        h0 = add(h0, a)
        h1 = add(h1, b)
        h2 = add(h2, c)
        h3 = add(h3, d)
        h4 = add(h4, e)
        h5 = add(h5, f)
        h6 = add(h6, g)
        h7 = add(h7, h)
    }

    digest = dim[32]
    q = {h0, h1, h2, h3, h4, h5, h6, h7}
    for (i = 0, i < 8, i=i+1)
    {
        digest[i*4]   = (q[i]>>24) & 0xff
        digest[i*4+1] = (q[i]>>16) & 0xff
        digest[i*4+2] = (q[i]>>8) & 0xff
        digest[i*4+3] = (q[i]) & 0xff
    }
    return(digest)
}

if (main==1)
{
    data = {0x68, 0x65, 0x6c, 0x6c, 0x6f, 0x20, 0x77, 0x6f, 0x72, 0x6c, 0x64}
    mydigest = Sha256(data)
    print("digest", endl)
    for (i=0, i < 32, i=i+1)
    {
        print(width=2, fill=0, hex, mydigest[i])
    }
    print(endl)
}
